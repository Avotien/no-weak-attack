plugins {
	id 'fabric-loom' version '1.13-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.7.1'
	id 'com.matthewprenger.cursegradle' version '1.4.0'
}

java{
sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21
}

base{
archivesName = project.archives_base_name
}

version = project.mod_version
group = project.maven_group

dependencies {
	// Fabric Dependencies
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.21.8 upwards uses Java 21.
	it.options.release = 21
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName.get()}"}
	}
}

curseforge {
	if (System.getenv("CURSE_API_KEY") != null) {
		apiKey = System.getenv("CURSE_API_KEY")

		project {
			id = "437915"
			releaseType = "release"

			changelogType = "markdown"
			changelog = System.getenv("CHANGELOG") ?: "https://github.com/florensie/no-weak-attack/blob/master/CHANGELOG.md"

			addGameVersion "1.21.8"
			addGameVersion "Fabric"

			mainArtifact(remapJar.archiveFile) {
				displayName = "${project.mod_version} for Minecraft 1.21.8"
			}

			afterEvaluate {
				uploadTask.dependsOn("build")
			}
		}
	}

	options {
		forgeGradleIntegration = false
	}
}
